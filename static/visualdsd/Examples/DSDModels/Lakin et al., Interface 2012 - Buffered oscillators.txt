directive simulation {initial=0; final=250000; points=1000; plots=[sum([<_ t^ A>]); sum([<_ t^ B>]); sum([<_ t^ C>]); {t^*}[K t^]:[A t^]<I>:[B t^]<K>:[Ch t^]<B>:[Dh t^]<B>:[J t^]:[I]; {t^*}[K t^]:[B t^]<I>:[C t^]<K>:[Ch t^]<C>:[Dh t^]<C>:[J t^]:[I]; {t^*}[K t^]:[C t^]<I>:[A t^]<K>:[Ch t^]<A>:[Dh t^]<A>:[J t^]:[I]]}
directive stochastic {scale = 100.000000; trajectories = 1}
directive deterministic {stiff=false; reltolerance=0.0001; abstolerance=1E-06}
def bind = 0.0003
def unbind = 0.1126
new t  @ bind, unbind
def Species(M, h, A) = M * <h t^ A>
def BJ2x2(Buffer, Fuel, Init, A, B, C, D) = 
  (new I
  new J
  new K
  new Ch
  new Dh
  ( Buffer * {t^*}[K t^]:[A t^]<I>:[B t^]<K>:[Ch t^]<C>:[Dh t^]<D>:[J t^]:[I]
  | Fuel * <t^ Ch t^ Dh t^ J>
  | Fuel * [J]{t^*}
  | Fuel * [K]{t^*}
  | (Init * 10.0) * <A t^ I>
  | Init * <B t^ K>))
def Buffer = 100.0
def Init = 10.0
def Fuel = 1000.0
new Ah 
new A 
new Bh 
new B 
new Ch 
new C 
( BJ2x2(Buffer,Fuel,Init,A,B,B,B)
| BJ2x2(Buffer,Fuel,Init,B,C,C,C)
| BJ2x2(Buffer,Fuel,Init,C,A,A,A)
| Species(2.0,Ah,A)
| Species(4.0,Bh,B)
| Species(2.0,Ch,C))